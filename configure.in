dnl ### Normal initialization. ######################################
AC_INIT
AC_CONFIG_SRCDIR([conserver/main.c])
AC_CONFIG_HEADER(config.h)

AC_DEFINE_UNQUOTED(CONFIGINVOCATION, "$0 $@")

dnl ### Set some option defaults. ###################################
if test -z "$CFLAGS"; then
  CFLAGS="-O"
fi
MKDIR="mkdir -p -m 755"
AC_SUBST(MKDIR)


dnl ### Custom settings. ############################################
ac_cv_allow_64bit=no
AC_MSG_CHECKING(whether to allow 64bit compilation)
AC_ARG_WITH(64bit,
	AC_HELP_STRING([--with-64bit],[Allow 64bit compilation]),
	[if test "$withval" = yes; then
		ac_cv_allow_64bit=yes
	fi])
AC_MSG_RESULT($ac_cv_allow_64bit)

AC_MSG_CHECKING(for port number specification)
AC_ARG_WITH(port,
	AC_HELP_STRING([--with-port=PORT],[Specify port number @<:@conserver@:>@]),
	[if test "$withval" != yes -a "$withval" != no; then
		AC_DEFINE_UNQUOTED(DEFPORT, "$withval")
		AC_MSG_RESULT(port '$withval')
	else
		AC_DEFINE_UNQUOTED(DEFPORT, "conserver")
		AC_MSG_RESULT(port 'conserver')
	fi],
	[AC_DEFINE_UNQUOTED(DEFPORT, "conserver")
	AC_MSG_RESULT(port 'conserver')])

AC_MSG_CHECKING(for secondary channel base port)
AC_ARG_WITH(base,
	AC_HELP_STRING([--with-base=PORT], [Base port for secondary channel @<:@0@:>@]),
	[if test "$withval" != yes -a "$withval" != no; then
		AC_DEFINE_UNQUOTED(DEFBASEPORT, "$withval")
		AC_MSG_RESULT(port '$withval')
	else
		AC_DEFINE_UNQUOTED(DEFBASEPORT, "0")
		AC_MSG_RESULT(port '0')
	fi],
	[AC_DEFINE_UNQUOTED(DEFBASEPORT, "0")
	AC_MSG_RESULT(port '0')])

AC_MSG_CHECKING(for master conserver hostname)
AC_ARG_WITH(master,
	AC_HELP_STRING([--with-master=MASTER],[Specify master server hostname @<:@console@:>@]),
	[if test "$withval" != yes; then
		AC_DEFINE_UNQUOTED(MASTERHOST, "$withval")
		AC_MSG_RESULT('$withval')
	else
		AC_DEFINE_UNQUOTED(MASTERHOST, "console")
		AC_MSG_RESULT('console')
	fi],
	[AC_DEFINE_UNQUOTED(MASTERHOST, "console")
	AC_MSG_RESULT('console')])

AC_MSG_CHECKING(for configuration filename)
AC_ARG_WITH(cffile,
	AC_HELP_STRING([--with-cffile=CFFILE],[Specify config filename @<:@conserver.cf@:>@]),
	[if test "$withval" != yes; then
		AC_DEFINE_UNQUOTED(CONFIGFILE, "$withval")
		AC_MSG_RESULT('$sysconfdir/$withval')
	else
		AC_DEFINE_UNQUOTED(CONFIGFILE, "conserver.cf")
		AC_MSG_RESULT('$sysconfdir/conserver.cf')
	fi],
	[AC_DEFINE_UNQUOTED(CONFIGFILE, "conserver.cf")
	AC_MSG_RESULT('$sysconfdir/conserver.cf')])

AC_MSG_CHECKING(for password filename)
AC_ARG_WITH(pwdfile,
	AC_HELP_STRING([--with-pwdfile=PWDFILE],[Specify password filename @<:@conserver.passwd@:>@]),
	[if test "$withval" != yes; then
		AC_DEFINE_UNQUOTED(PASSWDFILE, "$withval")
		AC_MSG_RESULT('$sysconfdir/$withval')
	else
		AC_DEFINE_UNQUOTED(PASSWDFILE, "conserver.passwd")
		AC_MSG_RESULT('$sysconfdir/conserver.passwd')
	fi],
	[AC_DEFINE_UNQUOTED(PASSWDFILE, "conserver.passwd")
	AC_MSG_RESULT('$sysconfdir/conserver.passwd')])

AC_MSG_CHECKING(for log filename)
AC_ARG_WITH(logfile,
	AC_HELP_STRING([--with-logfile=LOGFILE],[Specify log filename @<:@/var/log/conserver@:>@]),
	[if test "$withval" != yes; then
		AC_DEFINE_UNQUOTED(LOGFILEPATH, "$withval")
		AC_MSG_RESULT('$withval')
	else
		AC_DEFINE_UNQUOTED(LOGFILEPATH, "/var/log/conserver")
		AC_MSG_RESULT('/var/log/conserver')
	fi],
	[AC_DEFINE_UNQUOTED(LOGFILEPATH, "/var/log/conserver")
	AC_MSG_RESULT('/var/log/conserver')])

AC_MSG_CHECKING(for PID filename)
AC_ARG_WITH(pidfile,
	AC_HELP_STRING([--with-pidfile=PIDFILE],[Specify PID filepath @<:@/var/run/conserver.pid@:>@]),
	[if test "$withval" != yes; then
		AC_DEFINE_UNQUOTED(PIDFILE, "$withval")
		AC_MSG_RESULT('$withval')
	else
		AC_DEFINE_UNQUOTED(PIDFILE, "/var/run/conserver.pid")
		AC_MSG_RESULT('/var/run/conserver.pid')
	fi],
	[AC_DEFINE_UNQUOTED(PIDFILE, "/var/run/conserver.pid")
	AC_MSG_RESULT('/var/run/conserver.pid')])

AC_MSG_CHECKING(for MAXMEMB setting)
AC_ARG_WITH(maxmemb,
	AC_HELP_STRING([--with-maxmemb=MAXMEMB],[Specify maximum consoles per process @<:@16@:>@]),
	[if test "$withval" != yes; then
		AC_DEFINE_UNQUOTED(MAXMEMB, $withval)
		AC_MSG_RESULT($withval)
	else
		AC_DEFINE_UNQUOTED(MAXMEMB, 16)
		AC_MSG_RESULT(16)
	fi],
	[AC_DEFINE_UNQUOTED(MAXMEMB, 16)
	AC_MSG_RESULT(16)])

AC_MSG_CHECKING(for connect() timeout)
AC_ARG_WITH(timeout,
	AC_HELP_STRING([--with-timeout=TIMEOUT],[Specify connect() timeout in seconds @<:@10@:>@]),
	[if test "$withval" -gt 0 -o "$withval" -lt 300; then
		AC_DEFINE_UNQUOTED(CONNECTTIMEOUT, $withval)
		AC_MSG_RESULT($withval)
	else
		AC_DEFINE_UNQUOTED(CONNECTTIMEOUT, 10)
		AC_MSG_RESULT(10)
	fi],
	[AC_DEFINE_UNQUOTED(CONNECTTIMEOUT, 10)
	AC_MSG_RESULT(10)])

dnl ### Check for compiler et al. ###################################
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
dnl ### Compiler characteristics. ##################################
AC_AIX
AC_PROG_CC_STDC
AC_C_CONST

AC_CHECK_SIZEOF(long)
if test "$ac_cv_sizeof_long" -gt 4; then
    if test "$ac_cv_allow_64bit" != "yes"; then
	AC_MSG_ERROR([cannot build on >32bit systems (to override use --with-64bit)])
    else
	AC_MSG_WARN([building a 64bit version of conserver - good luck!])
    fi
fi

if test "$ac_cv_prog_cc_stdc" != "no"; then
    AC_DEFINE_UNQUOTED(USE_ANSI_PROTO, 1)
fi

dnl ### Checks for header files. ###################################
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/ioctl.h)

dnl ### POSIX terminal I/O
AC_CHECK_HEADERS(termios.h)
if test "$ac_cv_header_termios_h" != "yes"; then
  dnl ### SysV terminal I/O
  AC_CHECK_HEADERS(termio.h)
  if test "$ac_cv_header_termio_h" != "yes"; then
    dnl ### BSD terminal I/O
    AC_CHECK_HEADERS(sgtty.h)
    if test "$ac_cv_header_sgtty_h" != "yes"; then
      AC_MSG_ERROR([no usable terminal interface detected])
    fi
  fi
fi

AC_CHECK_HEADERS(unistd.h getopt.h sys/vlimit.h sys/resource.h ttyent.h sys/ttold.h sys/uio.h sys/proc.h sys/ioctl_compat.h usersec.h sys/select.h stropts.h sys/audit.h shadow.h sys/time.h crypt.h sysexits.h types.h)
AC_HEADER_TIME
AC_HEADER_SYS_WAIT
AC_TYPE_MODE_T
AC_TYPE_SIGNAL

AC_CACHE_CHECK([if sig_atomic_t is defined], ac_cv_type_sig_atomic_t,
  AC_TRY_LINK([
    #include <signal.h>
  ], [
    sig_atomic_t sigatom;
    sigatom = 1;
  ],
    ac_cv_type_sig_atomic_t=yes,
    ac_cv_type_sig_atomic_t=no
  )
)
if test "$ac_cv_type_sig_atomic_t" != "yes"; then
  AC_DEFINE(sig_atomic_t, volatile int, [Define if <signal.h> does not define sig_atomic_t])
fi

AC_CHECK_TYPES(in_addr_t,,,[$ac_includes_default
#include <netinet/in.h>])


dnl ### Check for libraries. #######################################
AC_SEARCH_LIBS(socket,socket)
AC_SEARCH_LIBS(gethostbyname,nsl)
AC_SEARCH_LIBS(crypt,crypt)

AC_SUBST(WRAPLIBS)
AC_SUBST(WRAPINCS)
AC_ARG_WITH(libwrap,
    AC_HELP_STRING([--with-libwrap@<:@=PATH@:>@],
	[Compile in libwrap (tcp_wrappers) support]),
    [if test "$with_libwrap" != "no"; then
	if test "$with_libwrap" != "yes"; then
	    WRAPCPPFLAGS="-I$with_libwrap/include"
	    WRAPLDFLAGS="-L$with_libwrap/lib"
	else
	    WRAPCPPFLAGS=""
	    WRAPLDFLAGS=""
	fi

	oCPPFLAGS="$CPPFLAGS"
	oLDFLAGS="$LDFLAGS"
	oLIBS="$LIBS"

	CPPFLAGS="$CPPFLAGS $WRAPCPPFLAGS"
	LDFLAGS="$LDFLAGS $WRAPLDFLAGS"

	AC_MSG_CHECKING(for TCP wrappers header tcpd.h)
	AC_CHECK_HEADER(tcpd.h,
	    [LIBS="$LIBS -lwrap"
	    AC_MSG_CHECKING(for TCP wrappers library -lwrap)
	    AC_TRY_LINK([#include <tcpd.h>
		int allow_severity = 0;
		int deny_severity  = 0;
		],[hosts_access((void *)0)],
		[AC_MSG_RESULT(yes)
		AC_DEFINE(USE_LIBWRAP)
		WRAPLIBS="$WRAPLDFLAGS -lwrap"
		WRAPINCS="$WRAPCPPFLAGS"],
		[AC_MSG_RESULT(no)])],)

	LIBS="$oLIBS"
	CPPFLAGS="$oCPPFLAGS"
	LDFLAGS="$oLDFLAGS"
    fi]
)


dnl ### Check for needed functions. ################################
AC_CHECK_FUNCS(getopt strerror getrlimit getsid setsid getuserattr setgroups tcgetpgrp tcsetpgrp tcgetattr tcsetattr tcsendbreak setpgrp getutent setttyent getspnam setlinebuf setvbuf ptsname grantpt unlockpt flock sigaction setsockopt getdtablesize putenv memset memcpy memcmp sysconf getpassphrase getlogin)
AC_FUNC_SETPGRP


dnl ### Create output files. #######################################
AC_SUBST(LIBOBJS)
AC_CONFIG_FILES([Makefile conserver/Makefile conserver.cf/Makefile console/Makefile autologin/Makefile])
AC_OUTPUT
